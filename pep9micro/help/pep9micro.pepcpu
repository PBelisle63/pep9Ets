//* This file contains a complete implementation of the microcode needed to execute
//* the Pep/9 instruction set. In order to test a working program,
//* Clone this file,

//*****************
//
// Instruction Specifier & Operand Specifier Fetch
//
//*****************

//First, decide if the program counter is even, and pick the appropriate fetch logic
start: if ISPCE uvnc_even else uvnc_odd

//*
//* Even Instruction Specifier fetch w/ prefetch
//*
uvnc_even: A=6, B=7, MARMux=1; MARCk
// Initiate fetch, PC <- PC plus 1.
MemRead, A=7, B=23, AMux=1, ALU=1, CMux=1, C=7; SCk, LoadCk
MemRead, A=6, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=6; LoadCk
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
// IR <- MDREven, T1 <- MDROdd, PrefetchValid <- true.
EOMux=0, AMux=0, ALU=0, CMux=1, C=8; LoadCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=11, PValid=1; LoadCk, PValidCk; goto end_is_fetch


//*
//* Odd Instruction Specifier fetch
//*
//Determine if prefetch in T1 is valid.
uvnc_odd: if ISPREFETCHVALID unvc_odd_v else unvc_odd_i

//Path taken when prefetch is valid. IR <- T1
unvc_odd_v: A=11, AMux=1, ALU=0, CMux=1, C=8; LoadCk
// PC <- PC plus 1.
A=7, B=23, AMux=1, ALU=1, CMux=1, C=7; SCk, LoadCk
A=6, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=6; LoadCk; goto end_is_fetch

//Path taken when prefetch is not valid. IR <- Mem[PC]<8..15>
// Initiate fetch, PC <- PC + 1.
unvc_odd_i: A=6, B=7, MARMux=1; MARCk
MemRead, A=7, B=23, AMux=1, ALU=1, CMux=1, C=7; SCk, LoadCk
MemRead, A=6, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=6; LoadCk
MemRead, MDROMux=0; MDROCk
//T1 <- MDROdd.
EOMux=1, AMux=0, ALU=0, CMux=1, C=8; LoadCk


//Instruction is fetched. Either prepare to execute unary instruction,
// or fetch operand specifier for non-unary instructions
end_is_fetch: if ISUNARY unary_dispatch else os_fetch
unary_dispatch: ISD //Start the unary execution path via the instruction specifier decoder.
//"A=5" is placeholder so that the assembler compiles this statement, since branches are currently not allowed
//on a line by themselves


//*
//* Operand Specifier Fetch
//*

//Branch if PC is even or odd
os_fetch: if ISPCE os_even else os_odd

//PC is even, so there is no prefetch to use. Go to Mem[PC]
os_even: A=6, B=7, MARMux=1; MARCk
// Initiate fetch, PC <- PC plus 2.
MemRead, A=7, B=24, AMux=1, ALU=1, CMux=1, C=7; SCk, LoadCk
MemRead, A=6, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=6; LoadCk
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
// OS <- MDREven & MDROdd
EOMux=0, AMux=0, ALU=0, CMux=1, C=9; LoadCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=10; LoadCk; AMD

// PC <- PC plus 2.
os_odd: A=7, B=24, AMux=1, ALU=1, CMux=1, C=7; SCk, LoadCk
A=6, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=6; LoadCk
//Initiate fetch from Mem[PC].
A=6, B=7, MARMux=1; MARCk
//OS<hi> <- T1.
MemRead, A=11, AMux=1, ALU=0, CMux=1, C=9; LoadCk
MemRead
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
// OS<lo> <- MDREven, T1 <- MDROdd, PrefetchValid <- true.
EOMux=0, AMux=0, ALU=0, CMux=1, C=10; LoadCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=11, PValid=1; LoadCk, PValidCk; AMD


//*****************
//
// Operand Specifier Decoder
//
//*****************
//Each addressing mode is responsible for the following:
//1. T6 <- Value of Operand Specifier after using the correct loads based on the addressing mode of the instruction
//2. T5 <- Address of the first byte of the location where the value in T6 came from. Immediate addressing does not need to do this.
//3. Using the ISD branch function to continue microprogram execution.

//Here is an example. Consider the instruction "LDWA 6,d" and suppose Mem[0x0006] = 0xCAFE.
//At the end of the "dAddr" unit, T6 <- 0xCAFE & T5 <- 0x0006.

//Another example. Consider the instruction LDWA 7,n and suppose:
//Mem[0x0007]=0xFE, Mem[0x0008]=0x01, Mem[0xFE01]=0xBE,Mem[0xFE02]=EF.
//At the of the "nAddr" unit, T6 <- 0xBEEF & T5 <- 0xFE01.

//Immediate Addressing. T6 <- OperSpec. T5 is ignored
iAddr: A=9, AMux=1, ALU=0, CMux=1, C=20; LoadCk
A=10, AMux=1, ALU=0, CMux=1, C=21; LoadCk; ISD


//*
//* Direct Addressing Mode & Derivatives (D & S, X, SX)
//*


//Direct Addressing Mode. T6 <- Mem[OperSpec], T5 <- OperSpec
dAddr: A=10, AMux=1, ALU=0, CMux=1, C=19; LoadCk
A=9, AMux=1, ALU=0, CMux=1, C=18; LoadCk
//MAR <- OperSpec, initiate fetch
A=9, B=10, MARMux=1; MARCk
//If OprSpec<lo> is odd,
MemRead, A=10, AMux=1, ALU=13; SCk; if BRS d_odd else d_even

d_odd: MemRead, A=10, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=9, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDROMux=0, C=16; LoadCk, MDROCk

//Shared between all "direct derivative" addressing modes.
//Assumes T4 is address of next byte to fetch, and MDRO contrinas T6<hi>
d_odd_shared: A=16, B=17, MARMux=1; MARCk
//T6<hi> <- MDRO
MemRead, EOMux=1, AMux=0, ALU=0, CMux=1, C=20; LoadCk
MemRead
MemRead, MDREMux=0; MDRECk
//T6<lo> <- MDRE
EOMux=0, AMux=0, ALU=0, CMux=1, C=21; LoadCk; ISD

//Shared between all "direct derivative" addressing modes. Loads an entire aligned word from mem into T6
d_even: MemRead
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
//T6 <- MDRE & MDRO
EOMux=0, AMux=0, ALU=0, CMux=1, C=20; LoadCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=21; LoadCk; ISD



//Stack Relative Addressing Mode. T6 <- Mem[OperSpec plus SP], T5 <- OperSpec plus SP.
sAddr: A=10, B=5, AMux=1, ALU=1, CMux=1, C=19; SCk, LoadCk
A=9, B=4, AMux=1, CSMux=1, ALU=2, CMux=1, C=18; LoadCk; goto sx_shared



//indexed Addressing Mode. T6 <- Mem[OperSpec plus X], T5 <- OperSpec plus X.
xAddr: A=10, B=3, AMux=1, ALU=1, CMux=1, C=19; SCk, LoadCk
A=9, B=2, AMux=1, CSMux=1, ALU=2, CMux=1, C=18; LoadCk; goto sx_shared



//Stack Indexed Addressing Mode. T6 <- Mem[OperSpec plus SP plus X], T5 <- OperSpec plus SP plus X.
sxAddr: A=10, B=5, AMux=1, ALU=1, CMux=1, C=19; SCk, LoadCk
A=9, B=4, AMux=1, CSMux=1, ALU=2, CMux=1, C=18; LoadCk
A=19, B=3, AMux=1, ALU=1, CMux=1, C=19; SCk, LoadCk
A=18, B=2, AMux=1, CSMux=1, ALU=2, CMux=1, C=18; LoadCk

//S, X, SX addressing modes all behave like D once the addition is performed.
//Determine if the memory address being fetched is even. If so goto d_even.
sx_shared: A=18, B=19, MARMux=1; MARCk
MemRead, A=19, AMux=1, ALU=13; SCk; if BRS sx_odd else d_even
//Else T4 <- T5 plus 1, MDRO <- Mem[T5]<lo> , goto d_odd_shared
sx_odd: MemRead, A=19, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=18, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDROMux=0, C=16; LoadCk, MDROCk; goto d_odd_shared


//*
//* Indirect Addressing Mode & Derivatives (N & SF)
//*

//Indirect Addressing Mode. T6 <- Mem[Mem[OperSpec]], T5 <- Mem[OperSpec].
//Initiate fetch Mem[OperSpec], branch if OperSpec is odd or even
nAddr: A=9, B=10, MARMux=1; MARCk
MemRead, A=10, AMux=1, ALU=13; SCk; if BRS n1_odd else n1_even

//Even path for first memread
n1_even: MemRead
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
//Determine if Mem[OperSpec] is even or odd
MARMux=0, EOMux=1, AMux=0, ALU=13; SCk, MARCk
//T5 <- Mem[OperSpec]
EOMux=0, AMux=0, ALU=0, CMux=1, C=18; LoadCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=19; LoadCk
MemRead; if BRS n2_odd else d_even

//Odd path for first memread
//Fetch Mem[OperSpec], T4 <- OperSpec plus 1,
n1_odd: MemRead, A=10, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=9, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDROMux=0, C=16; LoadCk, MDROCk
//Fetch Mem[T4]
n1_odd_shared: A=16, B=17, MARMux=1; MARCk
//T5<hi> <- MDRO
MemRead, EOMux=1, AMux=0, ALU=0, CMux=1, C=18; LoadCk
MemRead
MemRead, MDREMux=0; MDRECk
//T5<lo> <- MDRE
EOMux=0, AMux=0, ALU=0, CMux=1, C=19; LoadCk
A=18, B=19, MARMux=1; MARCk
//If T5<lo> is even, remaining fetch is identical to d_even.
MemRead, A=19, AMux=1, ALU=13; SCk; if BRS n2_odd else d_even

//Otherwise T4 <- T5 plus 1; goto d_odd_shared
n2_odd: MemRead, A=19, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=18, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDROMux=0, C=16; LoadCk, MDROCk; goto d_odd_shared


//Stack Deferred Addressing Mode. T6 <- Mem[Mem[OperSpec plus SP]], T5 <- Mem[OperSpec plus SP].
//T4 <- OperSpec plus SP
sfAddr: A=10, B=5, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
A=9, B=4, AMux=1, CSMux=1, ALU=2, CMux=1, C=16; LoadCk
//Fetch Mem[T4]
A=16, B=17, MARMux=1; MARCk
//If T4<lo> is even, remaining fetch is identical to n1_even.
MemRead, A=17, AMux=1, ALU=13; SCk; if BRS sf_odd else n1_even
//Otherwise MDRO <- Mem[T4]<lo>; T4 <- T4 plus 1; goto n1_odd_shared
sf_odd: MemRead, A=17, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=16, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDROMux=0, C=16; LoadCk, MDROCk; goto n1_odd_shared


//*
//* Stack Deferred Indexed T6 <- Mem[Mem[OperSpec plus SP] plus X], T5 <- Mem[OperSpec plus SP] plus X.
//*
//Unfortunately, this code shares little with other addressing modes due to the multiple additions


//T4 <- OperSpec plus SP
sfxAddr: A=10, B=5, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
A=9, B=4, AMux=1, CSMux=1, ALU=2, CMux=1, C=16; LoadCk
//Fetch Mem[T4]
A=16, B=17, MARMux=1; MARCk
//Branch if T4<lo> is odd
MemRead, A=17, AMux=1, ALU=13; SCk; if BRS sfx1_odd else sfx1_even

//Even path for first memread
sfx1_even: MemRead
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
//T5 <- MDRE plus X<hi> plus cin & MDRO plus X<lo>
B=3, EOMux=1, AMux=0, ALU=1, CMux=1, C=19; SCk, LoadCk
B=2, EOMux=0, AMux=0, CSMux=1, ALU=2, CMux=1, C=18; LoadCk; goto sfx1_shared

//Odd path for first memread
//T4 <- T4 plus 1
sfx1_odd: MemRead, A=17, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=16, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDROMux=0, C=16; LoadCk, MDROCk
//Fetch Mem[T4]
A=16, B=17, MARMux=1; MARCk
MemRead
MemRead
MemRead, MDREMux=0; MDRECk
//T5 <- MDRO plus X<hi> plus cin & MDRE plus X<lo>
B=3, EOMux=0, AMux=0, ALU=1, CMux=1, C=19; SCk, LoadCk
B=2, EOMux=1, AMux=0, CSMux=1, ALU=2, CMux=1, C=18; LoadCk

//Prepare for second memread
sfx1_shared: A=18, B=19, MARMux=1; MARCk
//If T4<lo> is even, remaining fetch is identical to d_even.
MemRead, A=19, AMux=1, ALU=13; SCk; if BRS sfx2_odd else d_even

//Otherwise T4 <- T4 plus 1; goto d_odd_shared
sfx2_odd: MemRead, A=19, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=18, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDROMux=0, C=16; LoadCk, MDROCk; goto d_odd_shared

//*****************
//
// Instruction implementation
//
//*****************


//*
//* Unclassified Unary Instructions
//*

//Stop Execution
stop: stop

// A <- SP
movspa: A=5, AMux=1, ALU=0, CMux=1, C=1; LoadCk
A=4, AMux=1, ALU=0, CMux=1, C=0; LoadCk; goto start

// A<8-11> <- 0, A<12-15> <- NZVC
movflga: CMux=0, C=1; LoadCk; goto start

// NZVC <- A<12-15>
movaflg: A=1, AMux=1, ALU=15, AndZ=0; NCk, ZCk, VCk, CCk; goto start

//Unary no operation
nop0: A=5; goto start


//*
//* Unary Logical Instructions
//*


//A <- ~A; N <- A<0, Z <- A=0
nota: A=1, AMux=1, ALU=10, AndZ=0, CMux=1, C=1; ZCk, LoadCk
A=0, AMux=1, ALU=10, AndZ=1, CMux=1, C=0; NCk, ZCk, LoadCk; goto start

//X <- ~X; N <- X<0, Z <- X=0
notx: A=3, AMux=1, ALU=10, AndZ=0, CMux=1, C=3; ZCk, LoadCk
A=2, AMux=1, ALU=10, AndZ=1, CMux=1, C=2; NCk, ZCk, LoadCk; goto start

//A <- -A; N <- A<0, Z <- A=0, V <- {Overflow}
nega: A=22, B=1, AMux=1, ALU=3, AndZ=0, CMux=1, C=1; ZCk, SCk, LoadCk
A=22, B=0, AMux=1, CSMux=1, ALU=4, AndZ=1, CMux=1, C=0; NCk, ZCk, VCk, LoadCk; goto start

//X <- -X; N <- X<0, Z <- X=0, V <- {Overflow}
negx: A=22, B=3, AMux=1, ALU=3, AndZ=0, CMux=1, C=3; ZCk, SCk, LoadCk
A=22, B=2, AMux=1, CSMux=1, ALU=4, AndZ=1, CMux=1, C=2; NCk, ZCk, VCk, LoadCk; goto start

//*
//* Unary Shift & Rotate Instructions
//*


//C <- A<0>, A<0..14> <- A<1..15>, A<15> <- 0; N <- A<0, Z <- A=0, V <- {Overflow}
asla: A=1, AMux=1, ALU=11, AndZ=0, CMux=1, C=1; ZCk, SCk, LoadCk
A=0, AMux=1, CSMux=1, ALU=12, AndZ=1, CMux=1, C=0; NCk, ZCk, VCk, CCk, LoadCk; goto start

//C <- X<0>, X<0..14> <- X<1..15>, X<15> <- 0; N <- X<0, Z <- X=0, V <- {Overflow}
aslx: A=3, AMux=1, ALU=11, AndZ=0, CMux=1, C=3; ZCk, SCk, LoadCk
A=2, AMux=1, CSMux=1, ALU=12, AndZ=1, CMux=1, C=2; NCk, ZCk, VCk, CCk, LoadCk; goto start

//C <- A<15>, A<1..15> <- A<0..14>; N <- A<0, Z <- A=0
asra: A=0, AMux=1, ALU=13, AndZ=0, CMux=1, C=0; NCk, ZCk, SCk, LoadCk
A=1, AMux=1, CSMux=1, ALU=14, AndZ=1, CMux=1, C=1; ZCk, CCk, LoadCk; goto start

//C <- X<15>, X<1..15> <- X<0..14>; N <- X<0, Z <- X=0
asrx: A=2, AMux=1, ALU=13, AndZ=0, CMux=1, C=2; NCk, ZCk, SCk, LoadCk
A=3, AMux=1, CSMux=1, ALU=14, AndZ=1, CMux=1, C=3; ZCk, CCk, LoadCk; goto start

//C <- A<0>, A<0..14> <- A<1..15>, A<15> <- C
rola: A=1, AMux=1, CSMux=0, ALU=12, CMux=1, C=1; SCk, LoadCk
A=0, AMux=1, CSMux=1, ALU=12, CMux=1, C=0; CCk, LoadCk; goto start

//C <- X<0>, X<0..14> <- X<1..15>, X<15> <- C
rolx: A=3, AMux=1, CSMux=0, ALU=12, CMux=1, C=3; SCk, LoadCk
A=2, AMux=1, CSMux=1, ALU=12, CMux=1, C=2; CCk, LoadCk; goto start

//C <- A<15>, A<1..15> <- A<0..14>, A<0> <- C
rora: A=0, AMux=1, CSMux=0, ALU=14, CMux=1, C=0; SCk, LoadCk
A=1, AMux=1, CSMux=1, ALU=14, CMux=1, C=1; CCk, LoadCk; goto start

//C <- X<15>, X<1..15> <- X<0..14>, X<0> <- C
rorx: A=2, AMux=1, CSMux=0, ALU=14, CMux=1, C=2; SCk, LoadCk
A=3, AMux=1, CSMux=1, ALU=14, CMux=1, C=3; CCk, LoadCk; goto start

//*
//* Non-Unary Branch Instructions
//*

//PC <- T6
br: A=21, AMux=1, ALU=0, CMux=1, C=7; LoadCk
A=20, AMux=1, ALU=0, CMux=1, C=6, PValid=0; LoadCk, PValidCk; goto start

brle: if BRLE br else start //N=1+Z=1 => (PC <- T6)
brlt: if BRLT br else start //N=1 => (PC <- T6)
breq: if BREQ br else start //N=1 => (PC <- T6)
brne: if BRNE br else start //N=0 => (PC <- T6)
brge: if BRGE br else start //N=0+Z=1 => (PC <- T6)
brgt: if BRGT br else start //N=0 => (PC <- T6)
brv: if BRV br else start //V=1 => (PC <- T6)
brc: if BRC br else start //C=1 => (PC <- T6)


//*
//* Non-Unary Arithetic Instruction
//*


//SP <- SP plus T6
addsp: A=5, B=21, AMux=1, ALU=1, CMux=1, C=5; SCk, LoadCk
A=4, B=20, AMux=1, CSMux=1, ALU=2, CMux=1, C=4; LoadCk; goto start

//SP <- SP minus T6
subsp: A=5, B=21, AMux=1, ALU=3, AndZ=0, CMux=1, C=5; SCk, LoadCk
A=4, B=20, AMux=1, CSMux=1, ALU=4, CMux=1, C=4; LoadCk; goto start


// A <- A plus T6; N <- A<0, Z <- A=0, V <- {Overflow}, C <- {Carry}
adda: A=1, B=21, AMux=1, ALU=1, AndZ=0, CMux=1, C=1; ZCk, SCk, LoadCk
A=0, B=20, AMux=1, CSMux=1, ALU=2, AndZ=1, CMux=1, C=0; NCk, ZCk, VCk, CCk, LoadCk; goto start

// X <- X plus T6; N <- A<0, Z <- A=0, V <- {Overflow}, C <- {Carry}
addx: A=3, B=21, AMux=1, ALU=1, AndZ=0, CMux=1, C=3; ZCk, SCk, LoadCk
A=2, B=20, AMux=1, CSMux=1, ALU=2, AndZ=1, CMux=1, C=2; NCk, ZCk, VCk, CCk, LoadCk; goto start

// A <- A minus T6; N <- A<0, Z <- A=0, V <- {Overflow}, C <- {Carry}
suba: A=1, B=21, AMux=1, ALU=3, AndZ=0, CMux=1, C=1; ZCk, SCk, LoadCk
A=0, B=20, AMux=1, CSMux=1, ALU=4, AndZ=1, CMux=1, C=0; NCk, ZCk, VCk, CCk, LoadCk; goto start

// X <- X minus T6; N <- A<0, Z <- A=0, V <- {Overflow}, C <- {Carry}
subx: A=3, B=21, AMux=1, ALU=3, AndZ=0, CMux=1, C=3; ZCk, SCk, LoadCk
A=2, B=20, AMux=1, CSMux=1, ALU=4, AndZ=1, CMux=1, C=2; NCk, ZCk, VCk, CCk, LoadCk; goto start

//*
//* Non-Unary Logical Instructions
//*

//A <- A*T6; N <- A<0, Z <- A=0
anda: A=1, B=21, AMux=1, ALU=5, AndZ=0, CMux=1, C=1; ZCk, LoadCk
A=0, B=20, AMux=1, ALU=5, AndZ=1, CMux=1, C=0; NCk, ZCk, LoadCk; goto start

//X <- X*T6; X <- A<0, Z <- X=0
andx: A=3, B=21, AMux=1, ALU=5, AndZ=0, CMux=1, C=3; ZCk, LoadCk
A=2, B=20, AMux=1, ALU=5, AndZ=1, CMux=1, C=2; NCk, ZCk, LoadCk; goto start

//A <- A+T6; N <- A<0, Z <- A=0
ora: A=1, B=21, AMux=1, ALU=7, AndZ=0, CMux=1, C=1; ZCk, LoadCk
A=0, B=20, AMux=1, ALU=7, AndZ=1, CMux=1, C=0; NCk, ZCk, LoadCk; goto start

//A <- A+T6; N <- X<0, Z <- X=0
orx: A=3, B=21, AMux=1, ALU=7, AndZ=0, CMux=1, C=3; ZCk, LoadCk
A=2, B=20, AMux=1, ALU=7, AndZ=1, CMux=1, C=2; NCk, ZCk, LoadCk; goto start

//*
//* Non-Unary Comparison Instructions
//*
cpwa: A=1, B=21, AMux=1, ALU=3, AndZ=0, CMux=1, C=15; ZCk, SCk, LoadCk
A=0, B=20, AMux=1, CSMux=1, ALU=4, AndZ=1, CMux=1, C=14; NCk, ZCk, VCk, CCk, LoadCk; if BRV cpw_n_fix else start

cpwx: A=3, B=21, AMux=1, ALU=3, AndZ=0, CMux=1, C=15; ZCk, SCk, LoadCk
A=2, B=20, AMux=1, CSMux=1, ALU=4, AndZ=1, CMux=1, C=14; NCk, ZCk, VCk, CCk, LoadCk; if BRV cpw_n_fix else start

cpw_n_fix: cmux=0, c=17; loadck
a=17, b=27, amux=1, alu=9, cmux=1, c=17; loadck
a=17, amux=1, alu=15, andz=0; Nck, Zck, Vck, Cck; goto start

cpba: A=1, B=21, AMux=1, ALU=3, AndZ=0, CMux=1, C=15; NCk, ZCk, VCk, CCk, LoadCk; goto start
cpbx: A=3, B=21, AMux=1, ALU=3, AndZ=0, CMux=1, C=15; NCk, ZCk, VCk, CCk, LoadCk; goto start

//*
//* Load Instructions
//*
//Load byte instructions have to decide which of the two bytes to use
//If immediate, use leftmost byte.
//Otherwise, if T5 is even, use right, else use left.
//Store the AAA field in t4
ldba: A=27, B=23, AMux=1, ALU=3, CMux=1, C=17; LoadCk
A=8, B=17, AMux=1, ALU=5, CMux=1, C=17; LoadCk
A=17, B=31, AMux=1, ALU=1; SCk; if BRS ldba_eo else ldba_i
ldba_eo: A=20, AMux=1, ALU=0, AndZ=0, CMux=1, C=1; NCk, ZCk, LoadCk; goto start
ldba_i: A=21, AMux=1, ALU=0, AndZ=0, CMux=1, C=1; NCk, ZCk, LoadCk; goto start
//ldba_z: A=22, AMux=1, ALU=0, CMux=1, C=0; LoadCk; goto start

ldbx: A=27, B=23, AMux=1, ALU=3, CMux=1, C=17; LoadCk
A=8, B=17, AMux=1, ALU=5, CMux=1, C=17; LoadCk
A=17, B=31, AMux=1, ALU=1; SCk; if BRS ldbx_eo else ldbx_i
ldbx_eo: A=20, AMux=1, ALU=0, AndZ=0, CMux=1, C=3; NCk, ZCk, LoadCk; goto start
ldbx_i: A=21, AMux=1, ALU=0, AndZ=0, CMux=1, C=3; NCk, ZCk, LoadCk; goto start
//ldbx_z: A=22, AMux=1, ALU=0, CMux=1, C=2; LoadCk; goto start

ldwa: A=21, AMux=1, ALU=0, AndZ=0, CMux=1, C=1; ZCk, LoadCk
A=20, AMux=1, ALU=0, AndZ=1, CMux=1, C=0; NCk, ZCk, LoadCk; goto start

ldwx: A=21, AMux=1, ALU=0, AndZ=0, CMux=1, C=3; ZCk, LoadCk
A=20, AMux=1, ALU=0, AndZ=1, CMux=1, C=2; NCk, ZCk, LoadCk; goto start

//*
//* Store Instructions
//*
//Store Byte Accumulate
stba: A=18, B=19, MARMux=1; MARCk
MemRead, A=19, AMux=1, ALU=13; SCk; if BRS stba_o else stba_e
stba_o: MemRead, A=1, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk; goto stbr_o
stba_e: MemRead, A=1, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk; goto stbr_e


stbx: A=18, B=19, MARMux=1; MARCk
MemRead, A=19, AMux=1, ALU=13; SCk; if BRS stbx_o else stbx_e
stbx_o: MemRead, A=3, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk; goto stbr_o
stbx_e: MemRead, A=3, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk; goto stbr_e

//Shared store byte ode
stbr_o: MemRead, MDREMux=0; MDRECk; goto mwrte
stbr_e: MemRead, MDROMux=0; MDROCk
mwrte: MemWrite
MemWrite
mwrte3: MemWrite; goto start


//Store word instructions
//First thing, determine if taking even or odd path
stwa: A=18, B=19, MARMux=1; MARCk
A=19, AMux=1, ALU=13; SCk; if BRS stwa_o else stwa_e

stwa_o: MemRead, A=19, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=18, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=16; LoadCk
MemRead, A=0, AMux=1, ALU=0, CMux=1, MDREMux=0, MDROMux=1; MDRECk, MDROCk
MemWrite, A=1, AMux=1, ALU=0, CMux=1, C=15; LoadCk; goto stwr_o

stwa_e: MemWrite, A=1, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk
MemWrite, A=0, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk; goto mwrte3


stwx: A=18, B=19, MARMux=1; MARCk
A=19, AMux=1, ALU=13; SCk; if BRS stwx_o else stwx_e

stwx_o: MemRead, A=19, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=18, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=16; LoadCk
MemRead, A=2, AMux=1, ALU=0, CMux=1, MDREMux=0, MDROMux=1; MDRECk, MDROCk
MemWrite, A=3, AMux=1, ALU=0, CMux=1, C=15; LoadCk; goto stwr_o

stwx_e: MemWrite, A=3, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk
MemWrite, A=2, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk; goto mwrte3

stwr_o: MemWrite
MemWrite
A=16, B=17, MARMux=1; MARCk
MemRead, A=15, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk
MemRead
MemRead, MDROMux=0; MDROCk
MemWrite
MemWrite
MemWrite; goto start


//*
//* Call & Return functions
//*
//First determine if SP is even
//If even, then take the aligned path
//Otherwise, take the unaligned path
call: A=5, AMux=1, ALU=13; SCk; if BRS call_o else call_e
//Decr SP by 2
call_e: A=5, B=24, AMux=1, ALU=3, CMux=1, C=5; SCk, LoadCk
A=4, B=22, AMux=1, CSMux=1, ALU=4, CMux=1, C=4; LoadCk
A=4, B=5, MARMux=1, PValid=0; MARCk, PValidCk
MemWrite, A=7, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk
MemWrite, A=6, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk
MemWrite, A=21, AMux=1, ALU=0, CMux=1, C=7; LoadCk
A=20, AMux=1, ALU=0, CMux=1, C=6; LoadCk; goto start

call_o: A=4, B=5, MARMux=1, PValid=0; MARCk, PValidCk
//Decr SP by 2
MemRead, A=5, B=24, AMux=1, ALU=3, CMux=1, C=5; SCk, LoadCk
MemRead, A=4, B=22, AMux=1, CSMux=1, ALU=4, CMux=1, C=4; LoadCk
MemRead, A=7, AMux=1, ALU=0, CMux=1, MDREMux=1, MDROMux=0; MDRECk, MDROCk
//Assign PC<hi> to T4<hi>, so that PC can be overriden earlier in the microcode
MemWrite, A=6, AMux=1, ALU=0, CMux=1, C=16; LoadCk
MemWrite, A=21, AMux=1, ALU=0, CMux=1, C=7; LoadCk
MemWrite, A=20, AMux=1, ALU=0, CMux=1, C=6; LoadCk
A=4, B=5, MARMux=1, PValid=0; MARCk, PValidCk
MemRead
MemRead
MemRead, A=16, AMux=1, ALU=0, CMux=1, MDREMux=0, MDROMux=1; MDRECk, MDROCk; goto mwrte


//This one is really really bad if unaligned
ret: A=4, B=5, MARMux=1, PValid=0; MARCk, PValidCk
A=5, AMux=1, ALU=13; SCk; if BRS ret_o else ret_e
ret_o: MemRead, A=5, B=24, AMux=1, ALU=1, CMux=1, C=5; SCk, LoadCk
MemRead, A=4, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=4; LoadCk
MemRead, MDROMux=0; MDROCk
A=4, B=5, MARMux=1; MARCk
MemRead
MemRead
MemRead, EOMux=1, AMux=0, ALU=0, CMux=1, MDREMux=0, C=6; LoadCk, MDRECk
EOMux=0, AMux=0, ALU=0, CMux=1, C=7; LoadCk; goto start
ret_e: MemRead, A=5, B=24, AMux=1, ALU=1, CMux=1, C=5; SCk, LoadCk
MemRead, A=4, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=4; LoadCk
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
EOMux=0, AMux=0, ALU=0, CMux=1, C=6; LoadCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=7; LoadCk; goto start
//TODO
//NZVC <- Sp
rettr: A=4, B=5, MARMux=1; MARCk
//T4<- SP +1
MemRead, A=5, B=23, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=4, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=16; LoadCk
MemRead, MDROMux=0; MDROCk

//Fetch A
A=16, B=17, MARMux=1; MARCk
//T4 <- T4 +2
MemRead, EOMux=1, AMux=0, ALU=15, AndZ=0; NCk, ZCk, VCk, CCk //Move NZVC
MemRead, A=17, B=24, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=16, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDREMux=0, MDROMux=0, C=16; LoadCk, MDRECk, MDROCk

//Fetch X
A=16, B=17, MARMux=1, EOMux=1, AMux=0, ALU=0, CMux=1, C=1; MARCk, LoadCk //Move A<lo>
MemRead, EOMux=0, AMux=0, ALU=0, CMux=1, C=0; LoadCk //Move A<hi>
//T4 <- T4 +2
MemRead, A=17, B=24, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=16, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDREMux=0, MDROMux=0, C=16; LoadCk, MDRECk, MDROCk

//Fetch PC
A=16, B=17, MARMux=1, EOMux=1, AMux=0, ALU=0, CMux=1, C=3; MARCk, LoadCk //Move X<lo>
MemRead, EOMux=0, AMux=0, ALU=0, CMux=1, C=2; LoadCk //Move X<hi>
//T4 <- T4 +2
MemRead, A=17, B=24, AMux=1, ALU=1, CMux=1, C=17; SCk, LoadCk
MemRead, A=16, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, MDREMux=0, MDROMux=0, C=16; LoadCk, MDRECk, MDROCk

//Fetch SP
A=16, B=17, MARMux=1, EOMux=1, AMux=0, ALU=0, CMux=1, C=7; MARCk, LoadCk //Move PC<lo>
MemRead, EOMux=0, AMux=0, ALU=0, CMux=1, C=6; LoadCk //Move PC<hi>
MemRead
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=5; LoadCk //Move PC<hi>
EOMux=0, AMux=0, ALU=0, CMux=1, C=4; LoadCk; goto start //Move PC <lo>


//trap instructions
nop1: goto trap
nop: goto trap
deci: goto trap
deco: goto trap
hexo: goto trap
stro: goto trap
//T4 <- Mem[0xFFF6]. Registers T1, T3,T4  are in use.
trap: A=31, B=29, MARMux=1, PValid=0; MARCk, PValidCk
MemRead
MemRead
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
EOMux=0, AMux=0, ALU=0, CMux=1, C=16; LoadCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=17; LoadCk

//Mem[T - 1] <- IR<0..7>
//Preserve T, Store T-1
A=16, B=17, MARMux=1; MARCk
//T4 <- (T - 3)
MemRead, A=17, B=25, AMux=1, ALU=3, CMux=1, C=17; SCk, LoadCk
MemRead, A=16, B=22, AMux=1, CSMux=1, ALU=4, CMux=1, C=16; LoadCk
MemRead, A=8, AMux=1, ALU=0, CMux=1, MDREMux=1, MDROMux=0; MDRECk, MDROCk
//T3 <- (T-5)
MemWrite, A=17, B=24, AMux=1, ALU=3, CMux=1, C=15; SCk, LoadCk
MemWrite, A=16, B=22, AMux=1, CSMux=1, ALU=4, CMux=1, C=14; LoadCk
MemWrite

//Mem[T-3] <- SP
A=16, B=17, MARMux=1; MARCk
MemWrite, A=5, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk
MemWrite, A=4, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk
//Start calculating whether IR is unary or nonuary trap
MemWrite, A=8, AMux=1, ALU=13, CMux=1, C=21; LoadCk

//Mem[T-5] <- PC
//NZVC bits go to T5<lo>
A=14, B=15, MARMux=1, CMux=0, C=19; MARCk, LoadCk
//Calculate if the IR is unary or nonunary, and increment the PC before pushing it onto the stack
A=21, AMux=1, ALU=13, CMux=1, C=21; LoadCk
A=21, B=23, AMux=1, ALU=1, CMux=1, C=21; LoadCk
A=21, B=27, AMux=1, ALU=1, AndZ=0, CMux=1, C=21; NCk, ZCk, LoadCk; if BRGE trap_pc2 else trap_wrtpc
trap_pc2: A=7, B=24, AMux=1, ALU=1, CMux=1, C=7; SCk, LoadCk
A=6, B=22, AMux=1, CSMux=1, ALU=2, CMux=1, C=6; LoadCk

//Write modified PC to stack

//Calculate lo order of T-7
trap_wrtpc: A=15, B=24, AMux=1, ALU=3, CMux=1, C=17; SCk, LoadCk
MemWrite, A=7, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk
MemWrite, A=6, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk
//Calculate hi order of t-7
MemWrite, A=14, B=22, AMux=1, CSMux=1, ALU=4, CMux=1, C=16; LoadCk


//Mem[T-7] <- X
A=16, B=17, MARMux=1; MARCk
MemWrite, A=3, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk
MemWrite, A=2, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk
//Calculate lo orde of T-9
MemWrite, A=17, B=24, AMux=1, ALU=3, CMux=1, C=17; SCk, LoadCk


//Mem[T-9] <- A
//Calculate hi order of T-9
A=16, B=22, AMux=1, CSMux=1, ALU=4, CMux=1, C=16; LoadCk
A=16, B=17, MARMux=1; MARCk
MemWrite, A=1, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk
MemWrite, A=0, AMux=1, ALU=0, CMux=1, MDREMux=1; MDRECk
//Calculate lo order of T-10 in sp
MemWrite, A=17, B=23, AMux=1, ALU=3, CMux=1, C=5; SCk, LoadCk


//Mem[T-10] <- NZVC
//Calculate high order of t-10
A=16, B=22, AMux=1, CSMux=1, ALU=4, CMux=1, C=4; LoadCk
A=4, B=5, MARMux=1; MARCk
MemWrite, A=19, AMux=1, ALU=0, CMux=1, MDROMux=1; MDROCk
MemWrite
MemWrite

//Load to PC
A=31, B=30, MARMux=1; MARCk
MemRead
MemRead
MemRead, MDREMux=0, MDROMux=0; MDRECk, MDROCk
EOMux=0, AMux=0, ALU=0, CMux=1, C=6; LoadCk
EOMux=1, AMux=0, ALU=0, CMux=1, C=7; LoadCk; goto start
